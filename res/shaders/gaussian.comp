#version 450
#extension GL_EXT_nonuniform_qualifier : require
layout(set = 0, binding = 0) uniform sampler2D imgInput;
layout(set = 0, binding = 1, rgba32f) uniform writeonly image2D imgOutput;
layout(set = 0, binding = 2) uniform ViewportInformation
{
    ivec4 extent; // width, height, width * height
} viewportInfo;
layout(set = 0, binding = 3) uniform GaussianBlurInformation
{
    uint blurRad;
} blurInfo;

layout(set = 0, binding = 4) uniform GaussianKernel
{
    vec4 kernalGroups[4]; // min offset of descriptorset is a mat4
} gaussianKernels[];

layout (local_size_x = 256, local_size_y = 1, local_size_z = 1) in;

vec4 GetKernelGroup(uint i)
{
    uint gaussianKernelsId = i / 16;
    uint kernalGroupsId = i % 16 / 4;
    return gaussianKernels[gaussianKernelsId].kernalGroups[kernalGroupsId];
}

// code from https://www.shadertoy.com/view/ctcBWM
void main()
{
    uint index = gl_GlobalInvocationID.x; 
    if (index >= viewportInfo.extent.z) return;
    vec2 coord = vec2(index % viewportInfo.extent.x, index / viewportInfo.extent.x);
    vec2 resolution = viewportInfo.extent.xy;
    vec2 uv = coord/resolution;
    vec2 fragSize = vec2(1)/resolution;

    vec3 col = -texture(imgInput, uv).rgb * gaussianKernels[0].kernalGroups[0].r; // we don't want to add (0, 0) twice  

    const uint kernelLength = blurInfo.blurRad + 1;
    const uint remainKernel = kernelLength % 4;
    for (uint i = 0; i < kernelLength - 3; i += 4)
    {
        vec4 kernelGroup = GetKernelGroup(i);
        col += texture(imgInput, uv + vec2(fragSize.x * float(i),     0.0f)).rgb * kernelGroup.r;
        col += texture(imgInput, uv + vec2(fragSize.x * float(i + 1), 0.0f)).rgb * kernelGroup.g;
        col += texture(imgInput, uv + vec2(fragSize.x * float(i + 2), 0.0f)).rgb * kernelGroup.b;
        col += texture(imgInput, uv + vec2(fragSize.x * float(i + 3), 0.0f)).rgb * kernelGroup.a;

        col += texture(imgInput, uv - vec2(fragSize.x * float(i),     0.0f)).rgb * kernelGroup.r;
        col += texture(imgInput, uv - vec2(fragSize.x * float(i + 1), 0.0f)).rgb * kernelGroup.g;
        col += texture(imgInput, uv - vec2(fragSize.x * float(i + 2), 0.0f)).rgb * kernelGroup.b;
        col += texture(imgInput, uv - vec2(fragSize.x * float(i + 3), 0.0f)).rgb * kernelGroup.a;
    }

    if (remainKernel == 1)
    {
        const uint offset = kernelLength / 4 * 4;
        vec4 partKernelGroup = GetKernelGroup(offset);

        col += texture(imgInput, uv + vec2(fragSize.x * float(offset), 0.0f)).rgb * partKernelGroup.r;
        col += texture(imgInput, uv - vec2(fragSize.x * float(offset), 0.0f)).rgb * partKernelGroup.r;
    }
    else if (remainKernel == 2)
    {
        const uint offset = kernelLength / 4 * 4;
        vec4 partKernelGroup = GetKernelGroup(offset);

        col += texture(imgInput, uv + vec2(fragSize.x * float(offset),     0.0f)).rgb * partKernelGroup.r;
        col += texture(imgInput, uv - vec2(fragSize.x * float(offset),     0.0f)).rgb * partKernelGroup.r;
        col += texture(imgInput, uv + vec2(fragSize.x * float(offset + 1), 0.0f)).rgb * partKernelGroup.g;
        col += texture(imgInput, uv - vec2(fragSize.x * float(offset + 1), 0.0f)).rgb * partKernelGroup.g;
    }
    else if (remainKernel == 3)
    {
        const uint offset = kernelLength / 4 * 4;
        vec4 partKernelGroup = GetKernelGroup(offset);

        col += texture(imgInput, uv + vec2(fragSize.x * float(offset),     0.0f)).rgb * partKernelGroup.r;
        col += texture(imgInput, uv - vec2(fragSize.x * float(offset),     0.0f)).rgb * partKernelGroup.r;
        col += texture(imgInput, uv + vec2(fragSize.x * float(offset + 1), 0.0f)).rgb * partKernelGroup.g;
        col += texture(imgInput, uv - vec2(fragSize.x * float(offset + 1), 0.0f)).rgb * partKernelGroup.g;
        col += texture(imgInput, uv + vec2(fragSize.x * float(offset + 2), 0.0f)).rgb * partKernelGroup.b;
        col += texture(imgInput, uv - vec2(fragSize.x * float(offset + 2), 0.0f)).rgb * partKernelGroup.b;
    }

    imageStore(imgOutput, ivec2(coord), vec4(col, 1.0f));
}