#version 450

#extension GL_EXT_mesh_shader : require
#extension GL_EXT_shader_8bit_storage: require
#extension GL_EXT_shader_explicit_arithmetic_types_int8 : require

// meshlets count each task shader workgroup processes
#define MESHLETS_PER_TASK_WORKGROUP 32
#define TASK_WORKGROUP_SIZE 32
#define MESHLETS_PER_MESH_WORKGROUP 1

const uint MESHLETS_PER_TASK_INVOCATION = (MESHLETS_PER_TASK_WORKGROUP + TASK_WORKGROUP_SIZE - 1) / TASK_WORKGROUP_SIZE;

struct Task
{
	uint baseMeshletId;
	uint8_t meshletIdOffset[MESHLETS_PER_TASK_WORKGROUP];
};
struct MeshletBounds
{
	vec4 boundSphere;
	vec4 coneApex;
	vec4 coneAxisCutoff;
};

layout(local_size_x = TASK_WORKGROUP_SIZE) in;
layout(set = 0, binding = 0) uniform CameraUBO
{
	mat4 view;
	mat4 proj;
	vec4 eye;
}cameraUBO;
layout(set = 0, binding = 1) uniform Frustum
{
	vec4 topFace;
	vec4 bottomFace;
	vec4 leftFace;
	vec4 rightFace;
	vec4 nearFace;
	vec4 farFace;
};
layout(set = 1, binding = 4) uniform ModelUBO
{
	mat4 model;
	mat4 inverseTransposeModel;
	float scaleFactor;
	uint meshletCount;
}modelUBO;
layout(set = 1, binding = 5) readonly buffer MeshletBoundsBuffer
{
	MeshletBounds meshletBounds[];
};

// Shared by task workgroup, and workgroups of mesh shader emit by EmitMeshTasksEXT
taskPayloadSharedEXT Task OUT;

// Shared by all task invocations within the one task workgroup
shared uint s_uMeshletCount;

bool EarlyCull(uint uMeshletID)
{
	MeshletBounds bounds = meshletBounds[uMeshletID];
	vec4 coneApex = modelUBO.model * bounds.coneApex;
	vec4 coneAxis = normalize(modelUBO.inverseTransposeModel * vec4(bounds.coneAxisCutoff.xyz, 0.0f));
	bool bFrustumCull = false; 
	bool bBackFaceCull = dot(normalize(coneApex.xyz - cameraUBO.eye.xyz), coneAxis.xyz) >= bounds.coneAxisCutoff.w;
	
	// Frustum culling
	if (!bBackFaceCull)
	{
		vec4 spherePos = modelUBO.model * vec4(bounds.boundSphere.xyz, 1.0);
		float radius = bounds.boundSphere.w * modelUBO.scaleFactor;
		bFrustumCull = 
			(dot(spherePos, topFace)       > radius) ||
			(dot(spherePos, bottomFace)    > radius) ||
			(dot(spherePos, leftFace)      > radius) ||
			(dot(spherePos, rightFace)     > radius) ||
			(dot(spherePos, nearFace)      > radius) ||
			(dot(spherePos, farFace)       > radius);
	}

	return bFrustumCull || bBackFaceCull;
}

void main()
{
	const uint uBaseID = gl_WorkGroupID.x * MESHLETS_PER_TASK_WORKGROUP;
	const uint uLaneID = gl_LocalInvocationID.x * MESHLETS_PER_TASK_INVOCATION;

	// Initialize the shared meshlet count for this task workgroup
	if (uLaneID == 0u)
	{
		s_uMeshletCount = 0u;
		OUT.baseMeshletId = uBaseID;
	}
	barrier();
	
	for (uint i = 0u; i < MESHLETS_PER_TASK_INVOCATION; i++)
	{
		uint uMeshletID = uBaseID + uLaneID + i; // the meshlet ID this invocation will process
		uint uCurrentMeshletIdSlot = 0u;         // the slot this invocation will write to in OUT.meshletIdOffset
		if (uMeshletID >= modelUBO.meshletCount)
			break;
		if (EarlyCull(uMeshletID))
			continue;
		uCurrentMeshletIdSlot = atomicAdd(s_uMeshletCount, 1u);
		OUT.meshletIdOffset[uCurrentMeshletIdSlot] = uint8_t(uLaneID + i);
	}
	barrier();

	// Defines the mesh workgroup size for this task workgroup (NOT task invocation)
	EmitMeshTasksEXT( 
		(s_uMeshletCount + MESHLETS_PER_MESH_WORKGROUP - 1) / MESHLETS_PER_MESH_WORKGROUP,
		1,
		1
	);
}
